
  CREATE OR REPLACE PACKAGE "CISADM"."CM_B914S_PKG" IS
TYPE CSR_TYPE IS REF CURSOR;
/**
================================================================================
PROGRAM NAME     : CM_B914S_PKG
CREATED BY       : 김일영
CREATED DATE     : 2020-07-16
LAST UPDATED BY  : 김일영
LAST UPDATE DATE :
PURPOSE          :  부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
VERSION          : 1.0
COMMENT          : 부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
================================================================================
CHANGE HISTORY
--------------------------------------------------------------------------------
DATE        NAME          DESCRIPTION
----------- ------------  ------------------------------------------------------
2020-07-16  김일영 최종작성
================================================================================
**/



/*================================================================================
PROGRAM NAME     : main_query
CREATED BY       : 김일영
CREATED DATE     : 2020-07-16
LAST UPDATED BY  : 김일영
LAST UPDATE DATE :
PURPOSE          :  부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
VERSION          : 1.0
COMMENT          :  부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
================================================================================*/
PROCEDURE main_query(

       O_CSR            OUT CSR_TYPE,
       O_ERR            OUT VARCHAR2,
       V_USERID         IN  VARCHAR2,   -- 필수 : 사용자 아이디
       V_LANG           IN  VARCHAR2,   -- 필수 : 시스템 언어
       V_IP             IN  VARCHAR2,   -- 필수 : 사용자 IP
       p_acct_id        IN  CHAR,--납부자번호
       p_tax_yymm       in varchar2,--부가세 년월
       p_charg_yymm     in varchar2 --청구년월
       
         
       )  ;


/*================================================================================
PROGRAM NAME     : process
CREATED BY       : 김일영
CREATED DATE     : 2020-08-21
LAST UPDATED BY  : 김일영
LAST UPDATE DATE :
PURPOSE          : 부가세 건에 대해 유형별로 처리를 수행한다.
VERSION          : 1.0
COMMENT          : 부가세 건에 대해 유형별로 처리를 수행한다.

=======================================================================================*/
procedure process
           (
             O_CSR         OUT CSR_TYPE
           , O_ERR            OUT VARCHAR2
           , V_USERID         IN  VARCHAR2   -- 필수 : 사용자 아이디
           , V_LANG           IN  VARCHAR2   -- 필수 : 시스템 언어
           , V_IP             IN  VARCHAR2   -- 필수 : 사용자 IP
           , p_acct_id        in  char  -- 납부자
           , p_tax_yymm       in varchar2--부가세 년월
           , p_charg_yymm     in varchar2 --청구년월
           , p_tax_crt_type   in varchar2 --변경유형
           , p_change_isu_dt  in varchar2 --변경 신고일자
           , p_cust_info_yn   in varchar2 --납부자 변경여부 
           , p_invoice_seq    in varchar2 --일련번호
           , p_remarks        in varchar2 --비고
     
);       


 /***************************************************************************************
  PROGRAM ID       :
  PROGRAM NAME     :  insert_btax_mod
  CREATED BY       :  김일영
  CREATION DATE    :  2020.07.20
  LAST UPDATED BY  :  
  LAST UPDATE DATE :  
  PURPOSE          :  기존 세금계산서 데이타에 대해 -수정세금계산서를 발행한다.
  VERSION  NO      :  1.0
  
  ******************************************************************************************/

  procedure insert_btax_mod (    p_charg_yymm    cm_bbill_transaction.cm_charg_yymm %type --청구년월
                                ,p_bill_cyc_cd   cm_bbill_transaction.bill_cyc_cd%type    --빌 cycle                                                  
                                ,p_acct_id       cm_bbill_transaction.acct_id%type        --납부자번호
                                ,p_seq           in out number   --일련번호
                                ,p_emp_cd        cm_btax_invoice.cm_created_id%type --사용자UID
                                ,p_gubun         in varchar2 --구분
                                ,p_adjust_dt     in date     --변경,조정일자
                                ,p_result_code   out varchar2 --return code
                                ,p_result_desc   out varchar2 --에러메시지
                                ,p_mody_code     varchar2 default '01' --수정코드
                                ,p_remarks       varchar2 
                             );      
             

  /***************************************************************************************
  PROGRAM ID       :
  PROGRAM NAME     :  btax_invoice_each_crt
  CREATED BY       :  김일영
  CREATION DATE    :  2020.07.20
  LAST UPDATED BY  :  
  LAST UPDATE DATE :  
  PURPOSE          :  p_gubun ==> 
                    AA: 폐업으로 인한 날짜 조정(-세금계산서 + 세금계산서(조정일자)
                    AC: 수신거부된 세금계산서 초기화 ( CM_TAX_INVOICE_YN 'N'으로 수정) (-세금계산서 +세금계산서 (간이 세금계산서))
                    AD: + 수정세금계산서 생성 (-수정세금계산서에 대한 + 세금계산서 생성)
                     
  VERSION  NO      :  1.0
  
   
  ******************************************************************************************/
  PROCEDURE  btax_invoice_each_crt ( p_rec   cm_btax_invoice%rowtype
                                    ,p_invoice_seq   number
                                    ,p_result_code   out varchar2
                                    ,p_result_desc   out varchar2
                                    ,p_gubun         in varchar2 
                                    ,p_emp_cd        cm_btax_invoice.cm_created_id%type  
                                    ,p_adjust_dt     in varchar2 
                                    ,p_mody_code     in varchar2 default '01'   
                                    ,p_remarks       in varchar2                      
                                   ) ;
                                                                                                                                                                           
END ;

CREATE OR REPLACE PACKAGE BODY "CISADM"."CM_B914S_PKG" IS
/**
================================================================================
PROGRAM NAME     : CM_B914S_PKG
CREATED BY       : 김일영
CREATED DATE     : 2020-07-16
LAST UPDATED BY  : 김일영
LAST UPDATE DATE :
PURPOSE          :  부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
VERSION          : 1.0
COMMENT          : 부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
================================================================================
CHANGE HISTORY
--------------------------------------------------------------------------------
DATE        NAME          DESCRIPTION
----------- ------------  ------------------------------------------------------
2020-07-16  김일영 최종작성
================================================================================
**/


/*================================================================================
PROGRAM NAME     : main_query
CREATED BY       : 김일영
CREATED DATE     : 2020-07-16
LAST UPDATED BY  : 김일영
LAST UPDATE DATE :
PURPOSE          :  부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
VERSION          : 1.0
COMMENT          :  부가세 데이타에 대한 수정(폐업,세금계산서초기화,수정세금계산서 생성)
================================================================================*/
PROCEDURE main_query(

       O_CSR            OUT CSR_TYPE,
       O_ERR            OUT VARCHAR2,
       V_USERID         IN  VARCHAR2,   -- 필수 : 사용자 아이디
       V_LANG           IN  VARCHAR2,   -- 필수 : 시스템 언어
       V_IP             IN  VARCHAR2,   -- 필수 : 사용자 IP
       p_acct_id        IN  CHAR,--납부자번호
       p_tax_yymm       in varchar2,--부가세 년월
       p_charg_yymm     in varchar2 --청구년월
       
         
       ) IS
   
begin
  
   
   --합산납부번호,차분,납부자명,상호,사업자등록번호,대표자명,업태,업종,부가세여부,주소,전입일
   open o_csr for
   select 
          regexp_replace(bi.cm_stax_yymm, '([0-9]{4})([0-9]{2})', '\1-\2') as CM_B100S_T1 -- 부가세년월
        , to_char(bi.cm_isu_dt, 'YYYY-MM-DD') as CM_B100S_T2 -- 작성일자
        , nvl(bi.cm_confirm_yn,'N') as CM_B100S_T3 -- 집계여부
        , substr(bi.cm_charg_yymm, 1, 4) ||'-'||substr( bi.cm_charg_yymm, 5, 2 )         CM_B100S_T4    -- 청구년월
        , bi.bill_cyc_cd           CM_B100S_T5   -- 차분
        , bi.cm_invoice_seq        CM_B100S_T6   -- 순번
        , (select bc.buyr_code
           from cm_btax_cust_info bc
           where bc.acct_id = bi.acct_id)     CM_B100S_T7   -- 종사업장 
        , ccv.descr   CM_B100S_T8   -- 사업장
        , bi.acct_id               CM_B100S_T9    -- 납부자번호
        , bi.cm_trans_dt                     CM_B100S_T10    --전송일시
        , DECODE(bi.cm_mod_invoice_yn,'Y','수정','일반')    CM_B100S_T11    --종류 : 수정/일반
        , (select t.DESCR from ci_lookup t
            where t.FIELD_NAME = 'CM_BTAX_AMENDE'
            and t.LANGUAGE_CD = V_LANG
            and t.FIELD_VALUE = cast(bi.cm_mody_code as char(4)))                     CM_B100S_T12   --재발행사유
        , to_char(cm_tot_sply_amt, '9,999,999,999,999,999,999,999,999,999,999,999,999,999,999')          CM_B100S_T13  -- NUMBER(22)     공급가액
        , to_char(cm_tot_stax, '9,999,999,999,999,999,999,999,999,999,999,999,999,999,999')              CM_B100S_T14  -- NUMBER(22)     부가세
        --, TO_CHAR(CM_TOT_SSUM, '9,999,999,999,999,999,999,999,999,999,999,999,999,999,999')              CM_B100S_T20  -- NUMBER(22)     합계
/*                  , replace(CM_DMAN_REG_NO,'-','')    CM_B100S_T11    --공급받는자_사업자번호
        , CM_DAMN_TRD_NM           CM_B100S_T12    -- 공급받는자_상호
        , BI.CM_ISSU_SEQNO         CM_B100S_T13   -- VARCHAR2(35)   Conversation_id
        ,(select m.approve_id from xxsb_dti_main@kdgas_prod_dl m where m.conversation_id = bi.cm_issu_seqno) as CM_B100S_T14   -- 승인번호
        , CM_DMAN_RPTV             CM_B100S_T15   -- 공급받는자_대표자
        , CM_DMAN_ADDR             CM_B100S_T16   -- 공급받는자_주소
        */
        , nvl(replace(c.byr_com_regno,'-',''),replace(cm_dman_reg_no,'-',''))    CM_B100S_T15    --공급받는자_사업자번호
        , nvl(c.byr_com_name, bi.cm_damn_trd_nm)           CM_B100S_T16    -- 공급받는자_상호
        , nvl(c.conversation_id, bi.cm_issu_seqno)         CM_B100S_T17   -- VARCHAR2(35)   Conversation_id
        , c.approve_id                                    as CM_B100S_T18   -- 승인번호
        , nvl(c.byr_rep_name,bi.cm_dman_rptv)             CM_B100S_T19   -- 공급받는자_대표자
        , nvl(c.byr_com_addr,bi.cm_dman_addr)            CM_B100S_T20   -- 공급받는자_주소                     

     FROM
          cm_btax_invoice  BI
        , xxsb_dti_main  c
        , cm_btax_master_vw B
        , ci_char_val_l     ccv

    WHERE 1 = 1
      AND bi.cm_stax_yymm  = REPLACE( p_tax_yymm, '-', '')
      and bi.cm_charg_yymm = REPLACE( p_charg_yymm, '-', '')
      and bi.bill_cyc_cd in ('01','02','03','04') 
      and bi.cm_issu_seqno = c.conversation_id(+)
      and bi.cm_enp_plc_cd=ccv.char_val
      and ccv.char_type_cd='CM_AREA'
      and ccv.language_cd=V_LANG
      and  bi.acct_id=nvl(p_acct_id,bi.acct_id)
      AND BI.ACCT_ID = B.ACCT_ID
      and nvl(bi.cm_tax_invoice_yn,'N') = 'Y'
      and (bi.cm_issu_seqno is not null or nvl(bi.cm_mod_invoice_yn,'N') = 'Y')
      and bi.cm_tot_stax <> 0
      order by cm_invoice_seq asc;
        
     
    
end;    


/*================================================================================
PROGRAM NAME     : process
CREATED BY       : 김일영
CREATED DATE     : 2020-08-21
LAST UPDATED BY  : 김일영
LAST UPDATE DATE :
PURPOSE          : 부가세 건에 대해 유형별로 처리를 수행한다.
VERSION          : 1.0
COMMENT          : 부가세 건에 대해 유형별로 처리를 수행한다.

=======================================================================================*/
procedure process
           (
             O_CSR         OUT CSR_TYPE
           , O_ERR            OUT VARCHAR2
           , V_USERID         IN  VARCHAR2   -- 필수 : 사용자 아이디
           , V_LANG           IN  VARCHAR2   -- 필수 : 시스템 언어
           , V_IP             IN  VARCHAR2   -- 필수 : 사용자 IP
           , p_acct_id        in  char  -- 납부자
           , p_tax_yymm       in varchar2--부가세 년월
           , p_charg_yymm     in varchar2 --청구년월
           , p_tax_crt_type   in varchar2 --변경유형
           , p_change_isu_dt  in varchar2 --변경 신고일자
           , p_cust_info_yn   in varchar2 --납부자 변경여부 
           , p_invoice_seq    in varchar2 --일련번호
           , p_remarks        in varchar2 --비고
)
IS


 
   x_invoice_seq number;
   x_adjust_dt     date :=trunc(sysdate);
   x_result_code  varchar2(1);
   x_result_desc  varchar2(500);
   x_bi_rec     cm_btax_invoice%rowtype;
   x_count      number;
   x_remarks    cm_btax_invoice.cm_rmk%type;
   x_progress   varchar2(200);
BEGIN
   /*p_tax_crt_type AA: 폐업으로 인한 날짜 조정(-세금계산서 + 세금계산서(조정일자)
                    AC: 수신거부된 세금계산서 초기화 ( CM_TAX_INVOICE_YN 'N'으로 수정) (-세금계산서 +세금계산서 (간이 세금계산서))
                    AD: + 수정세금계산서 생성 (-수정세금계산서에 대한 + 세금계산서 생성)
                    AF: 미발생분 세금계산서 생성*/
   x_progress :='001-세금계산서 정보조회';              
   begin                 
      select *
      into   x_bi_rec
      from   cm_btax_invoice bi
      where  bi.acct_id=p_acct_id
      and    bi.cm_charg_yymm=p_charg_yymm
      and    bi.cm_stax_yymm =p_tax_yymm
      and    bi.cm_invoice_seq in(select max(cm_invoice_seq)
                                  from  cm_btax_invoice
                                  where  bi.cm_charg_yymm=cm_charg_yymm
                                  and    bi.cm_stax_yymm=cm_stax_yymm
                                  and    bi.acct_id=acct_id
                                  and    bi.cm_tax_invoice_yn in('N','Y'));  
   exception 
       when no_data_found then                               
            o_err :='err ' || x_progress || ' 세금계산서에 대한 정보가 존재하지 않습니다';
        when others then
           o_err :='err ' || x_progress || '세금계산서에 대한 정보에 읽기중 에러가 발행했습니다:' || sqlerrm;
           return; 
                                  
   end;                               
   x_invoice_seq :=x_bi_rec.cm_invoice_seq ;
   if p_remarks is not null then
      x_remarks :=p_remarks;
      
   elsif p_tax_crt_type ='AA' then
      x_remarks :='세금계산서 발행일자 조정';
   elsif p_tax_crt_type='AC' then
      x_remarks :='세금계산서 거부로 인한 초기화';
   else
      x_remarks :='수정세금계산서에 대한 발행요청';
   end if; 
   if p_tax_crt_type in('AA','AC') then --폐업으로 인한 날짜 조정,세금계산서 거부
      if p_tax_crt_type ='AC' or (p_tax_crt_type='AA' and x_bi_rec.cm_trans_yn='N') then --미전송일때
         update cm_btax_invoice bi
         set    bi.cm_tax_invoice_yn='T'
               ,bi.cm_rmk=x_remarks
               ,bi.cm_mody_code=p_tax_crt_type
         where  bi.acct_id=p_acct_id
         and    bi.cm_charg_yymm=p_charg_yymm
         and    bi.bill_cyc_cd=bill_cyc_cd
         and    bi.cm_invoice_seq=cm_invoice_seq;
         x_invoice_seq :=x_invoice_seq + 1;
         x_adjust_dt := x_bi_rec.cm_isu_dt;
      else -- (-세금계산서 발행)
         x_adjust_dt := trunc(sysdate);  
         x_progress :='002-(-)수정세금계산서 생성';  
         insert_btax_mod(p_charg_yymm => p_charg_yymm
                     ,p_bill_cyc_cd => x_bi_rec.bill_cyc_cd
                     ,p_acct_id => p_acct_id
                     ,p_seq => x_invoice_seq
                     ,p_emp_cd => V_USERID
                     ,p_gubun => '80'
                     ,p_adjust_dt => x_adjust_dt
                     ,p_result_code => x_result_code
                     ,p_result_desc => x_result_desc
                     ,p_mody_code => p_tax_crt_type
                     ,p_remarks => x_remarks);
         x_invoice_seq := x_invoice_seq + 1;            
         if x_result_code <> '0' then
            rollback;
            o_err :='err ' || x_progress || x_result_desc;
            return;
         end if;
                                  
      end if; 
      
   end if;   
   
   if p_tax_crt_type='AA' then--발행일자 변경
      x_bi_rec.cm_isu_dt :=to_date(replace(p_change_isu_dt,'-',''),'yyyymmdd');
      
   elsif p_tax_crt_type='AC' then --세금계산서 거부
      x_bi_rec.cm_tax_invoice_yn :='N';
   else  --+ 수정세금계산서 생성 (-수정세금계산서에 대한 + 세금계산서 생성)
      --현재분에 대한 세금계산서 상태 체크
      if x_bi_rec.cm_tax_invoice_yn='Y' then
         o_err :='err 세금계산서 발행분이 생성되어 있습니다';
         return;
      end if;
      if p_cust_info_yn='N' then
         x_bi_rec.cm_invoice_seq :=p_invoice_seq;--해당 일련번호에 해당하는 사업자정보로 생성
      end if;   
      update cm_btax_invoice bi
      set    bi.cm_tax_invoice_yn='T'
            ,bi.cm_rmk=x_remarks
            ,bi.cm_mody_code=p_tax_crt_type
      where  bi.acct_id=p_acct_id
      and    bi.cm_charg_yymm=p_charg_yymm
      and    bi.bill_cyc_cd=bill_cyc_cd
      and    bi.cm_invoice_seq=cm_invoice_seq;   
      x_adjust_dt := x_bi_rec.cm_isu_dt;
   end if;           
   x_progress :='003-(+)수정세금계산서 생성'; 
   btax_invoice_each_crt(p_rec => x_bi_rec
                        ,p_invoice_seq => x_invoice_seq
                        ,p_result_code => x_result_code
                        ,p_result_desc => x_result_desc
                        ,p_gubun => '80'
                        ,p_emp_cd => V_USERID
                        ,p_adjust_dt => x_adjust_dt
                        ,p_mody_code => p_tax_crt_type
                        ,p_remarks=>x_remarks);
                                            
   if x_result_code <> '0' then
      rollback;
      o_err :='err ' || x_progress || x_result_desc;
      return;
   end if;
   
end;   

 /***************************************************************************************
  PROGRAM ID       :
  PROGRAM NAME     :  insert_btax_mod
  CREATED BY       :  김일영
  CREATION DATE    :  2020.07.20
  LAST UPDATED BY  :  
  LAST UPDATE DATE :  
  PURPOSE          :  기존 세금계산서 데이타에 대해 -수정세금계산서를 발행한다.
  VERSION  NO      :  1.0
  
  ******************************************************************************************/
  procedure insert_btax_mod (    p_charg_yymm    cm_bbill_transaction.cm_charg_yymm %type --청구년월
                                ,p_bill_cyc_cd   cm_bbill_transaction.bill_cyc_cd%type    --빌 cycle                                                  
                                ,p_acct_id       cm_bbill_transaction.acct_id%type        --납부자번호
                                ,p_seq           in out number   --일련번호
                                ,p_emp_cd        cm_btax_invoice.cm_created_id%type --사용자UID
                                ,p_gubun         in varchar2 --구분
                                ,p_adjust_dt     in date     --변경,조정일자
                                ,p_result_code   out varchar2 --return code
                                ,p_result_desc   out varchar2 --에러메시지
                                ,p_mody_code     varchar2 default '01' --수정코드
                                ,p_remarks       varchar2 
                             ) is    
                             
   
   x_bill_id          cm_bbill_transaction.bill_id%type;
   
  begin

     p_result_code := 0; -- 0: 정상, 1: 오류   
    
     
      -- bill_id 확인
      -- 2017.02.07  수송용은 한 acct_id 다중으로 존재       
     begin 
        select t.bill_id
        into x_bill_id
        from cm_bbill_transaction t
       where t.cm_charg_yymm = p_charg_yymm
         and t.bill_cyc_cd = p_bill_cyc_cd
         and t.acct_id = p_acct_id
         and rownum = 1;
     exception       
       when no_data_found then
         p_result_code := 1;   
         p_result_desc := '빌이 존재하지 않습니다.';
         raise_application_error(-20001,p_result_desc); 
     end;        

     insert into cm_btax_invoice
        (cm_charg_yymm                 /*청구년월*/
             ,bill_cyc_cd                   /*차분*/
             ,cm_invoice_seq                /*순번*/
             ,acct_id                       /*납부자번호*/
             ,cm_gubun                      /*구분:20-소급/30-전출/40-조정(간이,마감전신고)/50-개별,시운전/60-재발행/70:조정(산업,수숑)/80-조정(마감후신고,민수용)/90:합산acct*/
             ,cm_stax_yymm                  /*부가세매출년월*/
             ,cm_enp_plc_cd                 /*사업장*/
             ,prem_id                       /**/
             ,cm_isu_dt                     /*작성일(Original 신고일자)*/
             ,cm_dl_dt                      /*수정신고시 해당일자 업데이*/
             ,cm_dman_reg_no                /*공급받는자_사업자번호*/
             ,cm_damn_trd_nm                /*공급받는자_상호*/
             ,cm_dman_rptv                  /*공급받는자_대표자*/
             ,cm_damn_zipcd                 /*공급받는자_우편번호*/
             ,cm_dman_addr                  /*공급받는자_주소*/
             ,cm_dman_bsn_st                /*공급받는자_업태*/
             ,cm_dman_inds_tp               /*공급받는자_업종*/
             ,cm_sply_reg_no                /*공급자_사업자번호*/
             ,cm_sply_trd_nm                /*공급자_상호*/
             ,cm_sply_rptv                  /*공급자_대표자*/
             ,cm_sply_zipcd                 /*공급자_우편번호*/
             ,cm_sply_addr                  /*공급자_주소*/
             ,cm_sply_bsn_st                /*공급자_업태*/
             ,cm_sply_inds_tp               /*공급자_업종*/
             ,cm_tot_sply_amt               /*공급가액*/
             ,cm_tot_stax                   /*부가세*/
             ,cm_tot_ssum                   /*합계*/
             ,cm_base_fee                   /*기본요금*/
             ,cm_use_fee1                   /*사용료1*/
             ,cm_use_fee2                   /*사용료2*/
             ,cm_use_ssum                   /*사용료합계*/
             ,cm_gmcf                       /*교체비용*/
             ,cm_stax1                      /*부가세1*/
             ,cm_stax2                      /*부가세2*/
             ,cm_discount_amt               /*할인금액*/
             ,cm_rmk                        /*비고*/
             ,cm_tax_invoice_yn             /*부가세신고여부*/
             ,cm_re_invoice_yn              /**/
             ,cm_rerating_yn                /*매출조정여부*/
             ,cm_hotbill_yn                 /*핫빌여부*/
             ,cm_hapsan_yn                  /*합산여부*/
             ,cm_hapsan_acct_id             /*합산대표납부자번호*/
             ,cm_ref_pay_no                 /*호참조_납부자번*/
             ,cm_issu_seqno                 /*size:24>35 (2014.10.18,SmartBill) 세금계산서발행번호*/
             ,cm_issu_id                    /**/
             ,cm_bfo_issu_seqno             /*size:24>35 (2014.10.18,SmartBill)수정시 이전 세금계산서발행번호*/
             ,cm_bfo_issu_id                /**/
             ,cm_trans_yn                   /*전송여부*/
             ,cm_trans_id                   /*전송자ID*/
             ,cm_trans_dt                   /*전송일시*/
             ,cm_mod_invoice_yn             /*수정세금계산서여부*/
             ,cm_mody_code                  /*수정코드*/
             ,cm_giro_yn                    /*겸용서식여부*/
             ,cm_confirm_yn                 /*집계여부*/
             ,cm_cust_cl_cd                 /*RES:민수용, IND:산업용, CNG:수송용*/
             ,cm_ind_complex_cd             /**/
             ,bill_id                       /**/
             ,cm_use_revenue_yymm           /*사용매출년월*/
             ,cm_attribute1                 /**/
             ,cm_attribute2                 /*부가세신고 취소여부(C:취소) <-- Original 레코드에 C Flag 처리 --> 취소에 대한 간이분 반영에 사용*/
             ,cm_attribute3                 /*비고*/
             ,cm_attribute4                 /*As-Is 합산대표 acct*/
             ,cm_attribute5                 /*수입부과환급금*/
             ,cm_created_id                 /**/
             ,cm_created_dttm               /**/
             ,cm_updated_id                 /**/
             ,cm_updated_dttm               /**/
             ,version                       /**/
             ,loc_information13             /*(20161205)공급자 종사업장번호(양산,충전소 등)*/
             )         
          select  cm_charg_yymm/*청구년월*/
                  ,bill_cyc_cd/*차수*/
                  ,p_seq + 1 /*순번-- seq 1 증가*/
                  ,acct_id /*납부자번호*/
                                                      /*부가세 매출년월*/
                  ,p_gubun    --구분
                  ,cm_stax_yymm /*부가세매출년월*/
                  ,cm_enp_plc_cd /*사업장*/
                  ,prem_id /*세대번호*/ 
                  ,cm_isu_dt
                  ,p_adjust_dt
                  ,byr_com_regno  /*공급받는자 사업자번호*/          
                  ,byr_com_name  /*공급받는자상호*/
                  ,byr_rep_name /*공급받는자대표자*/
                  ,cm_damn_zipcd /*공급받는자우편번호*/
                  ,byr_com_addr /*공급받는자주소*/
                  ,cm_dman_bsn_st /*공급받는자업태*/
                  ,byr_com_classify /*공급받는자업종*/
                  ,cm_sply_reg_no/*공급자사업자번호*/
                  ,cm_sply_trd_nm/*공급자상호*/
                  ,cm_sply_rptv/*공급자대표자*/
                  ,cm_sply_zipcd/*공급자우편번호*/
                  ,cm_sply_addr/*공급자주소*/
                  ,cm_sply_bsn_st/*공급자업태*/
                  ,cm_sply_inds_tp/*공급자업종*/
                  ,-1*cm_tot_sply_amt/*공급금액*/
                  ,-1*cm_tot_stax/*부가세금액*/
                  ,-1*cm_tot_ssum/*합계액*/
                  ,-1*cm_base_fee/*기본요금*/
                  ,-1*cm_use_fee1/*사용료1*/
                  ,-1*cm_use_fee2/*사용료2*/
                  ,-1*cm_use_ssum/*사용료합계*/
                  ,-1*cm_gmcf/*교체비용*/
                  ,-1*cm_stax1/*부가세1*/
                  ,-1*cm_stax2/*부가세2*/
                  ,-1*cm_discount_amt/*할인금액*/
                  ,p_remarks/*비고*/
                  ,cm_tax_invoice_yn/*부가세신고여부*/
                  ,cm_re_invoice_yn/*재발행여부*/
                  ,decode(p_gubun,'3','Y','N') AS cm_rerating_yn /*매출조정여부*/
                  ,cm_hotbill_yn /*핫빌여부*/
                  ,cm_hapsan_yn /*합산여부*/
                  ,cm_hapsan_acct_id /*합산대표 납부자번호*/
                  ,cm_ref_pay_no /*참조 납부자번호*/
                  ,null as cm_issu_seqno  /*size:24>35 (2014.10.18,SmartBill) 세금계산서발행번호*/
                  ,cm_issu_id 
                  ,cm_issu_seqno as cm_bfo_issu_seqno
                  ,cm_bfo_issu_id
                  ,'N' as cm_trans_yn /*전송여부*/
                  ,null as cm_trans_id /*전송자ID*/
                  ,null as cm_trans_dt /*전송일시*/
                  --,decode(p_gubun,'3',decode(x_tax_yn,'Y',decode(cm_trans_yn,'Y','Y','N'),'N'),'Y') as cm_mod_invoice_yn /*수정세금계산서여부*/
                  ,'Y'
                  --,decode(p_gubun,'3',decode(x_tax_yn,'Y',decode(x_close_yn,'Y','01',null),null),'01') as cm_mody_code
                  ,p_mody_code /*수정코드*/
                  ,cm_giro_yn /*겸용서식여부*/
                  ,'N' as cm_confirm_yn /*집계여부*/
                  ,cm_cust_cl_cd /* 민수용,산업용구분*/
                  ,cm_ind_complex_cd 
                  ,bill_id     /*청구ID*/                         
                  ,cm_use_revenue_yymm  /*사용매출년월*/
                  ,cm_attribute1 
                  ,cm_attribute2  /*부가세신고 취소여부(C:취소) <-- Original 레코드에 C Flag 처리 --> 취소에 대한 간이분 반영에 사용*/
                  ,cm_attribute3 /*비고*/
                  ,cm_attribute4 /* AS-is 합산대표 acct*/
                  ,cm_attribute5 /*수입부과환급금*/
                  ,nvl(p_emp_cd,'SYSUSER') /* 생성자*/
                  ,sysdate   /*생성일시*/
                  ,nvl(p_emp_cd,'SYSUSER') /*수정자*/ 
                  ,sysdate /*수정일시*/
                  ,version /*버젼*/
                  ,LOC_INFORMATION13  /*(20161205)공급자 종사업장번호(양산,충전소 등)*/
            from  cm_btax_invoice a, 
                  xxsb_dti_main b
           where  1 = 1
             and  cm_charg_yymm = p_charg_yymm
             and  bill_cyc_cd = p_bill_cyc_cd               
             and  acct_id = p_acct_id
             and  cm_invoice_seq = p_seq 
             and  a.cm_issu_seqno = b.conversation_id(+)
             ; 
             
        
          p_seq := 1 + p_seq;
     
                  
  exception 
     when others then 
       rollback; 
       p_result_code := 1;
       p_result_desc := '수정 세금계산서 내역이 생성되지 않았습니다.'||SQLERRM;
  end insert_btax_mod;  
  
 /***************************************************************************************
  PROGRAM ID       :
  PROGRAM NAME     :  btax_invoice_each_crt
  CREATED BY       :  김일영
  CREATION DATE    :  2020.07.20
  LAST UPDATED BY  :  
  LAST UPDATE DATE :  
  PURPOSE          :  p_gubun ==> 
                    AA: 폐업으로 인한 날짜 조정(-세금계산서 + 세금계산서(조정일자)
                    AC: 수신거부된 세금계산서 초기화 ( CM_TAX_INVOICE_YN 'N'으로 수정) (-세금계산서 +세금계산서 (간이 세금계산서))
                    AD: + 수정세금계산서 생성 (-수정세금계산서에 대한 + 세금계산서 생성)
                     
  VERSION  NO      :  1.0
  
   
  ******************************************************************************************/
  PROCEDURE  btax_invoice_each_crt ( p_rec   cm_btax_invoice%rowtype
                                    ,p_invoice_seq   number
                                    ,p_result_code   out varchar2
                                    ,p_result_desc   out varchar2
                                    ,p_gubun         in varchar2 
                                    ,p_emp_cd        cm_btax_invoice.cm_created_id%type  
                                    ,p_adjust_dt     in varchar2 
                                    ,p_mody_code     in varchar2 default '01'   
                                    ,p_remarks       in varchar2                      
                                   ) is
                    
   x_bi_rec           cm_btax_invoice%rowtype; 
   x_invoice_out_seq number;
   x_invoice_seq number:=p_invoice_seq;
   x_img_gubun   varchar2(1):='1';
   
BEGIN
   
   p_result_code := 0; -- 0: 정상, 1: 오류    
   
  
    for bi_rec in ( 
                   select bt.cm_charg_yymm 
                            ,bt.bill_cyc_cd 
                            ,bt.acct_id
                            ,bt.per_id
                            ,bt.prem_id
                            ,bt.bill_dt
                            ,bt.cm_area
                            ,bt.cm_use_revenue_yymm as use_revenue_yymm 
                            ,bt.cm_tax_revenue_yymm as stax_yymm 
                            ,a.cm_biz_reg_no  as dman_reg_no 
                            ,a.cm_trade_nm as damn_trd_nm 
                            ,a.cm_rptv as dman_rptv 
                            ,trim(c.postal) as damn_zipcd 
                            ,a.cm_biz_addr_yn  
                            ,a.etax_addr_yn
                            ,a.etax_addr 
                            ,a.cm_biz_address    
                            ,a.cm_biz_conditions as dman_bsn_st
                            ,a.cm_ind_type as dman_inds_tp                                                                                                          
                            ,nvl(bt.cm_basic_amt,0) as base_fee 
                            ,nvl(bt.cm_pur_use_amt1,0)  as use_fee1 
                            ,nvl(bt.cm_pur_use_amt2,0) as use_fee2                           
                            ,nvl(bt.cm_exchange_amt,0) as gmcf 
                            ,nvl(bt.cm_late_fee_amt,0) as add_fee 
                            ,nvl(bt.cm_tax_amt1,0) as stax1 
                            ,nvl(bt.cm_tax_amt2,0) as stax2  
                            ,nvl(bt.cm_discount_amt,0)  as cm_discount_amt -- 할인금액  
                            ,nvl(-1*bt.cm_oil_refund_amt,0)  as cm_oil_refund_amt                           
                            ,nvl(a.cm_tax_invoice_yn,'N') as tax_invoice_yn
                            ,nvl(bt.cm_hotbill_yn,'N') as hotbill_yn
                            ,bt.bill_id                                           
                            ,bt.cust_cl_cd   
                            ,trim(bt.cm_payc) as cm_payc   
                            ,nvl(a.giro_yn,'Y') giro_yn  
                            ,nvl(bt.trial_yn,'N') trial_yn                      
                    from (select * 
                              from cm_bbill_transaction bill 
                             where 1 = 1
                               and bill.bill_id =  p_rec.bill_id 
                               and not exists ( select * 
                                                  from ci_acct_char ac
                                                 where ac.char_type_cd = 'CM_SEGR'
                                                   and ac.acct_id = bill.acct_id)) bt
                        ,cm_btax_master_vw a
                       -- ,ci_acct b
                        ,ci_prem c 
                    where 1 = 1
                    and bt.acct_id = a.acct_id(+)
                    and bt.prem_id = c.prem_id 
                       
 ) loop
          
            
         
        x_bi_rec                           := p_rec;       
        
        x_bi_rec.cm_dl_dt                  := p_adjust_dt;  
        x_bi_rec.cm_stax_yymm :=to_char(x_bi_rec.cm_isu_dt,'yyyymm');
        x_bi_rec.cm_charg_yymm             :=bi_rec.cm_charg_yymm;
        x_bi_rec.cm_mod_invoice_yn         := 'Y';   --수정세금계산서여부 
        x_bi_rec.cm_invoice_seq            := p_invoice_seq;
        x_bi_rec.cm_rmk                    := p_remarks;
             /* 민수용 정보변경시 사유코드변경(20190411)*/
               --x_bi_rec.cm_mody_code              := '01';
        x_bi_rec.cm_mody_code              := p_mody_code;
        x_bi_rec.cm_gubun                  := p_gubun; -- 수정(민수용)
        x_bi_rec.cm_bfo_issu_seqno         := p_rec.cm_issu_seqno;   
  
        x_bi_rec.cm_dman_reg_no               := bi_rec.dman_reg_no;          
        x_bi_rec.cm_damn_trd_nm               := bi_rec.damn_trd_nm;      
        x_bi_rec.cm_dman_rptv                 := bi_rec.dman_rptv; 
        x_bi_rec.cm_damn_zipcd                := bi_rec.damn_zipcd;
        
        if trim(bi_rec.cm_biz_addr_yn) = 'Y' then
          x_bi_rec.cm_dman_addr  := bi_rec.cm_biz_address;
        else
           begin
            select decode(trim(f.bill_addr_srce_flg),
                          'ACOV',
                          (trim(g.geo_code) || ' ' || trim(g.address1) || ' ' ||
                          trim(g.address4) || ' ' || trim(g.num1) || ' ' ||
                          decode(trim(g.num2), '', '', '-' || trim(g.num2))),
                          'PREM',
                          (trim(e.geo_code) || ' ' || trim(e.address1) || ' ' ||
                          trim(e.address4) || ' ' || trim(e.num1) || ' ' ||
                          decode(trim(e.num2), '', '', '-' || trim(e.num2)))) || ' ' ||
                   decode(trim(f.bill_addr_srce_flg),
                          'ACOV',
                          g.city,
                          'PREM',
                          (select srch_char_val
                             from ci_prem_char
                            where char_type_cd = 'CM_HO_NM'
                              and prem_id = e.prem_id))
              into x_bi_rec.cm_dman_addr                
              from ci_acct c, ci_prem e, ci_acct_per f, ci_per_addr_ovrd g
             where 1 = 1
               and c.mailing_prem_id = e.prem_id
               and c.acct_id = f.acct_id
               and f.acct_id = g.acct_id(+)
               and c.acct_id = bi_rec.acct_id;
           exception
             when others then
               null;
           end;        
        end if;
        
     
          
        x_bi_rec.cm_dman_bsn_st               := bi_rec.dman_bsn_st;                       
        x_bi_rec.cm_dman_inds_tp              := bi_rec.dman_inds_tp;
        x_bi_rec.cm_base_fee                  := bi_rec.base_fee;
        x_bi_rec.cm_use_fee1                  := bi_rec.use_fee1;  
        x_bi_rec.cm_use_fee2                  := bi_rec.use_fee2;
        x_bi_rec.cm_use_ssum                  := bi_rec.use_fee1 + bi_rec.use_fee2;
        x_bi_rec.cm_gmcf                      := bi_rec.gmcf;
        x_bi_rec.cm_stax1                     := bi_rec.stax1;  -- 부가세1
        x_bi_rec.cm_stax2                     := bi_rec.stax2;  -- 부가세2
        x_bi_rec.cm_discount_amt              := bi_rec.cm_discount_amt; -- 할인금액(-)
        x_bi_rec.cm_attribute5                := bi_rec.cm_oil_refund_amt;  -- 오일환급금(+)
        x_bi_rec.cm_tot_sply_amt              := bi_rec.base_fee + bi_rec.use_fee1 + bi_rec.use_fee2 + bi_rec.gmcf + bi_rec.cm_discount_amt; -- 공급가액
        x_bi_rec.cm_tot_stax                  := bi_rec.stax1 + bi_rec.stax2; -- 부가세 = 부가세1+부가세2
        x_bi_rec.cm_tot_ssum                  := bi_rec.base_fee + bi_rec.use_fee1 + bi_rec.use_fee2 + bi_rec.gmcf + bi_rec.cm_discount_amt + bi_rec.stax1 + bi_rec.stax2;
      
        x_bi_rec.cm_re_invoice_yn             := 'Y';       
        x_bi_rec.cm_trans_yn                  := 'N'; 
        

        -- 겸용서식여부 
        x_bi_rec.cm_giro_yn := bi_rec.giro_yn;
        x_bi_rec.cm_issu_seqno             := null;
        x_bi_rec.cm_issu_id                := null;
        x_bi_rec.cm_created_id             := p_emp_cd;
        x_bi_rec.cm_created_dttm           := sysdate;
        x_bi_rec.cm_updated_id             := p_emp_cd;
        x_bi_rec.cm_updated_dttm           := sysdate;
        x_bi_rec.version                   := 1;
        x_bi_rec.cm_confirm_yn             := 'N';      
        
         
                                                   
       insert into cm_btax_invoice
       values x_bi_rec;
      
 
       cm_bimage_pkg.cm_b040s_each_crt ( p_yyyymm      => x_bi_rec.cm_charg_yymm         
                                        ,p_bill_cyc_cd => x_bi_rec.bill_cyc_cd    
                                        ,p_bill_id     => x_bi_rec.bill_id   
                                        ,p_acct_id     => x_bi_rec.acct_id   
                                        ,p_gubun       => '3' 
                                        ,p_seq         => x_invoice_seq 
                                        ,p_result_code => p_result_code 
                                        ,p_result_desc => p_result_desc
                                        ,p_pmt_mthd    => bi_rec.cm_payc  
                                        ,p_img_gubun   => x_img_gubun  
                                        ,p_out_seq     => x_invoice_out_seq
                                        ,p_emp_cd      => p_emp_cd 
                                        ,p_balance_yn  => 'Y'                                                          
                                      ); 
                     
    end loop; 
       
  exception 
    when others then 
      
      p_result_code := 1;   
      p_result_desc := substr(p_rec.cm_charg_yymm,1,4)||'-'||substr(p_rec.cm_charg_yymm,5,2)||'월분 '||'세금계산서가 생성되지 않았습니다.' || sqlerrm||p_result_desc; 
      rollback;            
  end btax_invoice_each_crt;

END ;
